bundle agent my_prep_cfe {
  vars:
      "add_root_crontab_for_cfe"
        slist =>
          { "15 * * * * /usr/bin/python /var/cfengine/modules/revive_cfengine.py $(my_definitions.infra_server[infra01.local.net])" };

  files:
    any::
      "$(my_definitions.cfe_staging_dir)/."
        create => "true",
        perms => mog("0755", "root", "root");

      "/usr/bin/query-enc"
        perms => mog("0755", "root", "root"),
        copy_from => no_backup_dcp("/var/cfengine/modules/query-enc.py");

    # there's no point in running cf-serverd/cf-monitord in clients
    centos.!cfe_mps::
      "/etc/sysconfig/cfengine3"
        perms => mog("0644", "root", "root"),
        copy_from => no_backup_dcp("$(my_definitions.files_repo_dir)/cfengine3"),
        classes => if_repaired("restart_cfengine");

    centos::
      "/var/spool/cron/root"
        comment => "revive cfengine if it's dead",
        create => "true",
        perms => mog("0600", "root", "root"),
        edit_line => append_if_no_line("$(add_root_crontab_for_cfe)");

  commands:
    centos_7.restart_cfengine::
      "/usr/bin/systemctl restart cfengine3"
        contain => silent;

    centos_6.restart_cfengine::
      "/sbin/service cfengine3 restart"
        contain => silent;

  reports:
    any::
      "cf3: CFEngine is taking care of this host, nice & easy!";
}
