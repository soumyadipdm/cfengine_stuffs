bundle agent my_manage_mps {
  # this bundle manages Master Policy Server
  # as well as ENC/ldap
  vars:
      "enc_process" string => "/export/content/openldap/libexec/slapd";

      "tools" slist => { "fabric", "dnsmasq" };

  classes:
    any::
      "activate_policy" or => { "cfe_mps", "infra01_local_net" };

  files:
    activate_policy::
      "/etc/hosts"
        perms => mog("0644", "root", "root"),
        copy_from => local_dcp("$(my_definitions.files_repo_dir)/hosts"),
        classes => if_repaired("etc_hosts_changed");

    centos_7.activate_policy::
      "/usr/lib/systemd/system/enc-openldap.service"
        perms => mog("0744", "root", "root"),
        copy_from => no_backup_dcp("$(my_definitions.files_repo_dir)/enc-openldap.service"),
        classes => if_repaired("enc_openldap_service_installed");

    activate_policy::
      "/etc/dnsmasq.conf"
        perms => mog("0644", "root", "root"),
        copy_from => local_dcp("$(my_definitions.files_repo_dir)/dnsmasq.conf"),
        classes => if_repaired("restart_enc_service");

    activate_policy::
      "/var/www/html/repo"
        link_from => ln_s("/mnt/data/repo"),
        move_obstructions => "true";

  packages:
    activate_policy::
      "$(tools)"
        package_policy => "add",
        package_method => yum_rpm_permissive,
        package_select => "==",
        package_architectures => { "x86_64" };

  processes:
    activate_policy::
      "$(enc_process)" restart_class => "restart_enc_service";

    activate_policy::
      "/usr/sbin/dnsmasq" restart_class => "restart_dnsmasq";

  commands:
    activate_policy.enc_openldap_service_installed::
      "/usr/bin/systemctl enable enc-openldap.service"
        classes => if_repaired("restart_enc_service");

    activate_policy.restart_enc_service::
      "/usr/bin/systemctl restart enc-openldap.service"
        classes => if_else("restart_enc_successful", "restart_enc_unsuccessful");

    activate_polciy.(etc_hosts_changed|restart_dnsmasq)::
      "/usr/bin/systemctl restart dnsmasq" classes => if_repaired("dnsmasq_restart_successful");

  reports:
    activate_policy.restart_enc_service::
      "cf3: ENC process is not running. Restarting it";

    activate_policy.enc_openldap_service_installed::
      "cf3: ENC/ldap service has been installed";

    activate_policy.restart_enc_successful::
      "cf3: ENC/ldap service is restarted";

    activate_policy.restart_enc_unsuccessful::
      "cf3: FATAL: ENC/ldap service restart failed";

    activate_policy.etc_hosts_changed::
      "cf3: /etc/hosts file has changed";

    activate_policy.dnsmasq_restart_successful::
      "cf3: dnsmasq is successfully restarted";
}
