bundle agent my_manage_mps {
  # this bundle manages Master Policy Server
  # as well as ENC/ldap
  vars:
      "enc_process" string => "/export/content/openldap/libexec/slapd";
      "enc_service_command"
        string =>
          "/export/content/openldap/libexec/slapd -u openldap -g openldap -h \"ldap:// ldaps://\"";

      "tools" slist => { "fabric" };

  classes:
    any::
      "activate_policy" or => { "cfe_mps", "infra01_local_net" };

  packages:
    activate_policy::
      "$(tools)"
        package_policy => "add",
        package_method => yum_rpm_permissive,
        package_select => "==",
        package_architectures => { "x86_64" };

  processes:
    activate_policy::
      "$(enc_process)" restart_class => "restart_enc_service";

  commands:
    activate_policy.restart_enc_service::
      "$(enc_service_command)" classes => if_else("restart_enc_successful",
                                                  "restart_enc_unsuccessful");

  reports:
    activate_policy.restart_enc_service::
      "cf3: ENC process is not running. Restarting it";

    activate_policy.restart_enc_successful::
      "cf3: ENC/ldap service is restarted";

    activate_policy.restart_enc_unsuccessful::
      "cf3: FATAL: ENC/ldap service restart failed";
}
