bundle agent my_monitoring {
  vars:
      "zabbix_packages" slist => { "zabbix-agent", "zabbix" };

      "ganglia_server_packages" slist => { "ganglia-gmetad", "ganglia-web" };
      "ganglia_client_packages" slist => {
                                         "ganglia",
                                         "ganglia-gmond",
                                         "ganglia-devel",
                                         "ganglia-gmond-python",
                                         };

  classes:
      "gmond_not_installed" not => fileexists("/usr/sbin/gmond");
      "gmetad_not_installed" not => fileexists("/usr/sbin/gmetad");

  files:
    cfe_mps::
      "/etc/ganglia/gmetad.conf"
        perms => mog("0644", "root", "root"),
        copy_from => local_dcp("$(my_definitions.files_repo_dir)/ganglia/gmetad.conf"),
        classes => if_repaired("gmetad_conf_changed");

    any::
      "/etc/ganglia/gmond.conf"
        perms => mog("0644", "root", "root"),
        copy_from => local_dcp("$(my_definitions.files_repo_dir)/ganglia/gmond.conf"),
        classes => if_repaired("gmond_conf_changed");

  packages:
    cfe_mps.!gmetad_not_installed::
      "$(ganglia_server_packages)"
        package_policy => "add",
        package_method => yum_rpm,
        package_architectures => { "x86_64" },
        classes => if_repaired("ganglia_server_packages_installed");

    centos.!gmond_not_installed::
      "$(ganglia_client_packages)"
        package_policy => "add",
        package_method => yum_rpm,
        package_version => "3.7.1-3.el7",
        package_select => "==",
        package_architectures => { "x86_64" },
        classes => if_repaired("ganglia_client_packages_installed");

    centos::
      "$(zabbix-packages)"
        package_policy => "delete",
        package_method => yum_rpm,
        package_select => "==",
        package_architectures => { "x86_64" };

  processes:
    cfe_mps::
      "/usr/sbin/gmetad" restart_class => "restart_gmetad";

    any::
      "/usr/sbin/gmond" restart_class => "restart_gmond";

  commands:
    cfe_mps::
      "/usr/bin/systemctl enable gmetad.service";

    cfe_mps.(gmetad_conf_changed|restart_gmetad)::
      "/usr/bin/systemctl restart gmetad.service";

    centos_7::
      "/usr/bin/systemctl enable gmond.service";
    centos_6::
      "/sbin/chkconfig gmond on";

    centos_7.(gmond_conf_changed|restart_gmond)::
      "/usr/bin/systemctl restart gmond.service";
    centos_6.(gmond_conf_changed|restart_gmond)::
      "/sbin/service gmond restart";

  reports:
    cfe_mps.gmetad_conf_changed::
      "cf3: /etc/ganglia/gmetad.conf has changed, restarting gmetad service";
    any.gmond_conf_changed::
      "cf3: /etc/ganglia/gmond.conf has changed, restarting gmond service";
}
