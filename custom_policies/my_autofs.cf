bundle agent my_autofs {
  vars:
      "auto_package" slist => { "autofs", "nfs-utils" };
      "autofs_files" slist => { "auto.master", "auto.direct" };

  files:
    cfe_mps::
      "/etc/$(autofs_files)"
        perms => mog("0644", "root", "root"),
        copy_from => no_backup_dcp("$(my_definitions.files_repo_dir)/$(autofs_files)"),
        classes => if_repaired("autofs_copied");

  packages:
    any::
      "$(auto_package)"
        package_method => generic,
        package_architectures => { "x86_64" },
        package_policy => "add";

  processes:
    cfe_mps::
      "/usr/sbin/automount" restart_class => "automount_not_running";

  commands:
    cfe_mps.(automount_not_running|autofs_copied)::
      "/usr/bin/systemctl restart autofs"
        contain => silent,
        classes => if_repaired("autofs_restarted");

  reports:
    cfe_mps.autofs_copied::
      "cf3: /etc/auto.master has changed";

    cfe_mps.autofs_restarted::
      "cf3: automount service has been restarted";
}
