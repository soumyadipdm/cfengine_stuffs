bundle agent my_autofs {
  vars:
    centos::
      "auto_package" slist => { "autofs", "nfs-utils" };

  files:
    centos::
      "/etc/auto.master"
        perms => mog("0644", "root", "root"),
        copy_from => no_backup_dcp("$(my_definitions.files_repo_dir)/auot.master"),
        classes => if_repaired("autofs_copied");

    centos.!cfe_mps::
      "/etc/auto.direct"
        perms => mog("0644", "root", "root"),
        copy_from => no_backup_dcp("$(my_definitions.files_repo_dir)/auto.direct_all"),
        classes => if_repaired("autofs_copied");

    cfe_mps::
      "/etc/auto.direct"
        perms => mog("0644", "root", "root"),
        copy_from => no_backup_dcp("$(my_definitions.files_repo_dir)/auto.direct_infra"),
        classes => if_repaired("autofs_copied");

  packages:
    centos::
      "$(auto_package)"
        package_policy => "add",
        package_method => yum_rpm_permissive,
        package_select => "==",
        package_architectures => { "x86_64" };

  processes:
    centos::
      "/usr/sbin/automount" restart_class => "automount_not_running";

  commands:
    centos_7.(automount_not_running|autofs_copied)::
      "/usr/bin/systemctl restart autofs"
        contain => silent,
        classes => if_repaired("autofs_restarted");

    centos_6.(automount_not_running|autofs_copied)::
      "/sbin/service autofs restart"
        contain => silent,
        classes => if_repaired("autofs_restarted");

  reports:
    cfe_mps.autofs_copied::
      "cf3: /etc/auto.master has changed";

    cfe_mps.autofs_restarted::
      "cf3: automount service has been restarted";
}
