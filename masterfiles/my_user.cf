bundle agent my_user {
  vars:
    any::
      "users" slist => { "unixuser" };
    any.!freebsd::
      "password[unixuser]"
        string =>
          "$6$zK3Swac98Lf4IoJ6$xns.AVKkePFqeJ.5F/mDMvwC1va56GLIC66pUy3Klcv5iJEKut3zYdd9ZwPux8QC6nGPBu93W.mlqXTOpXPoK0";

    freebsd::
      "password[unixuser]"
        string =>
          "$6$CUubOkVF5q2s.eOd$ZhfXZrK8nGtehCJNuqES5Omv.mN.o6Iy7kLvwFeCcq3WYguztyeW4PhSBhVSOSVOSgV.lV2GFH.9q557oaYfx0";

    any::
      "privileged_users" slist => { "unixuser" };
      "PS1_prompt"
        string =>
          "export PS1=\"\[\e[00;31m\]{\[\e[0m\]\[\e[00;36m\]\u\[\e[0m\]\[\e[00;33m\]@\[\e[0m\]\[\e[00;36m\]\h\[\e[0m\]\[\e[00;31m\]}\[\e[0m\]\[\e[00;34m\][\[\e[0m\]\[\e[00;32m\]\W\[\e[0m\]\[\e[00;37m\]|\[\e[0m\]\[\e[00;32m\]\A\[\e[0m\]\[\e[00;34m\]]\[\e[0m\]\[\e[00;31m\]->\[\e[0m\]\[\e[00;37m\] \[\e[0m\]\"";

    linux|freebsd::
      "homedir"
        string => "/home/",
        policy => "free";

    solaris::
      "homedir"
        string => "/export/home/",
        policy => "free";

    freebsd::
      "pwline[unixuser]"
        string => "unixuser:$(password[unixuser]):1001:1001::0:0:User &:/home/unixuser:/bin/bash";

    centos|solaris::
      "sudoers_dir" string => "/etc/sudoers.d";

    freebsd::
      "sudoers_dir" string => "/usr/local/etc/sudoers.d";

  users:
    # sadly users promise is not supported on FreeBSD
    !freebsd.generic_host::
      "$(users)"
        policy => "present",
        home_dir => "$(homedir)$(users)",
        password => hashed_password("$(password[$(users)])"),
        shell => "/bin/bash",
        # uid => "500",
        classes => if_repaired("$(users)_created");

  files:
    freebsd.generic_host::
      "/home/$(users)/."
        create => "true",
        perms => mog("0750", "$(users)", "$(users)");

    freebsd.generic_host::
      "/etc/master.passwd"
        perms => mog("0600", "root", "wheel"),
        edit_line => append_if_no_line("$(pwline[$(users)])"),
        classes => if_repaired("$(users)_created");

    generic_host::
      "$(sudoers_dir)/$(privileged_users)"
        perms => mog("0440", "root", "root"),
        create => "true",
        edit_defaults => empty,
        edit_line => insert_user("$(privileged_users)"),
        classes => if_repaired("$(privileged_users)_sudoers_repaired");

    generic_host::
      "$(homedir)$(users)/.bashrc"
        create => "true",
        perms => mog("0740", "$(users)", "$(users)"),
        edit_line => insert_lines("$(PS1_prompt)");

  commands:
    freebsd.generic_host::
      "/usr/sbin/pw user add $(users)" classes => if_repaired("$(users)_created_step1");

    freebsd.generic_host::
      "/usr/bin/chsh -s /bin/bash $(users)" classes => if_repaired("$(users)_created_step2");

  reports:
    generic_host::
      "cf3: $(users) is created" ifvarclass => "$(users)_created_step1";

    generic_host::
      "cf3: /etc/sudoers.d/$(privileged_users) sudoers installed"
        ifvarclass => "$(privileged_users)_sudoers_repaired";
}

bundle edit_line insert_user(user) {
  insert_lines:
      "$(user)    ALL=(ALL) ALL";
}
