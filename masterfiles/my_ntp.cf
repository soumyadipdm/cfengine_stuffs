bundle agent my_ntp {
  vars:
    linux::
      "ntp_conf_file" string => "/etc/ntp.conf";

    centos.cfe_mps::
      "ntp_servers" slist => { "0.in.pool.ntp.org", "0.asia.pool.ntp.org", "0.asia.pool.ntp.org" };

    centos.cfe_mps::
      "interface" string => "0.0.0.0";

    centos.!cfe_mps::
      "ntp_servers" slist => { "app01.local.net" };

    centos.!cfe_mps::
      "interface" string => "127.0.0.1";

    centos::
      "ntp_packages" slist => { "ntp", "ntpdate" };
      "timezone[ZONE]" string => "Asia/Kolkata";

  files:
    any::
      "/etc/cfe.d/."
        create => "true",
        perms => mog("0755", "root", "root");

    centos::
      "/etc/cfe.d/ntp_conf.mustache"
        perms => mog("0644", "root", "root"),
        copy_from => local_dcp("/var/cfengine/inputs/config_files_repo/ntp_conf.mustache"),
        classes => if_repaired("ntp_conf_template_changed");

    centos|ntp_conf_template_changed::
      "/etc/cfe.d/ntp.conf.stg"
        create => "true",
        edit_defaults => empty,
        edit_template => "/etc/cfe.d/ntp_conf.mustache",
        template_method => "mustache",
        classes => if_repaired("ntp_conf_template_expanded");

    centos::
      "/etc/ntp.conf"
        perms => mog("0644", "root", "root"),
        copy_from => no_backup_dcp("/etc/cfe.d/ntp.conf.stg"),
        classes => if_repaired("ntp_conf_changed");

    centos::
      "/etc/sysconfig/clock"
        perms => mog("0644", "root", "root"),
        create => "true",
        edit_line => set_quoted_values("timezone");

    centos::
      "/etc/localtime"
        link_from => ln_s("/usr/share/zoneinfo/Asia/Kolkata"),
        move_obstructions => "true";

  packages:
    centos::
      "$(ntp_packages)"
        package_method => generic,
        package_policy => "add";

  processes:
    centos::
      "ntpd" restart_class => "restart_ntpd";

  services:
    centos::
      "ntpd"
        service_policy => "start",
        classes => if_repaired("ntpd_restarted");

  commands:
    centos.(restart_ntpd|ntp_conf_changed)::
      "/sbin/service ntpd restart" classes => if_repaired("ntpd_restarted");

  reports:
    ntpd_restarted::
      "cf3: ntpd has been restarted";
}
