bundle agent my_ntp {
  vars:
    any::
      "ntp_conf_file" string => "/etc/ntp.conf";

    cfe_mps::
      "ntp_servers" slist => { "0.in.pool.ntp.org", "0.asia.pool.ntp.org", "0.asia.pool.ntp.org" };

    cfe_mps::
      "interface" string => "0.0.0.0";

    any.!cfe_mps::
      "ntp_servers" slist => { "app01.local.net" };

    any.!cfe_mps::
      "interface" string => "127.0.0.1";

    any::
      "ntp_packages" slist => { "ntp", "ntpdate" };
      "timezone[ZONE]" string => "Asia/Kolkata";

    centos::
      "service_cmd" string => "/sbin/service";

    freebsd::
      "service_cmd" string => "/usr/sbin/service";

  files:
    any::
      "/etc/cfe.d/."
        create => "true",
        perms => mog("0755", "root", "root");

    any::
      "/etc/cfe.d/ntp_conf.mustache"
        perms => mog("0644", "root", "root"),
        copy_from => local_dcp("/var/cfengine/inputs/config_files_repo/ntp_conf.mustache"),
        classes => if_repaired("ntp_conf_template_changed");

    any|ntp_conf_template_changed::
      "/etc/cfe.d/ntp.conf.stg"
        create => "true",
        edit_defaults => empty,
        edit_template => "/etc/cfe.d/ntp_conf.mustache",
        template_method => "mustache",
        classes => if_repaired("ntp_conf_template_expanded");

    any::
      "/etc/ntp.conf"
        perms => mog("0644", "root", "root"),
        copy_from => no_backup_dcp("/etc/cfe.d/ntp.conf.stg"),
        classes => if_repaired("ntp_conf_changed");

    any::
      "/etc/sysconfig/clock"
        perms => mog("0644", "root", "root"),
        create => "true",
        edit_line => set_quoted_values("timezone");

    any::
      "/etc/localtime"
        link_from => ln_s("/usr/share/zoneinfo/Asia/Kolkata"),
        move_obstructions => "true";

  packages:
    centos::
      "$(ntp_packages)"
        package_method => generic,
        package_policy => "add";

  processes:
    any::
      "ntpd" restart_class => "restart_ntpd";

  services:
    centos::
      "ntpd"
        service_policy => "start",
        classes => if_repaired("ntpd_restarted");

  commands:
    any.(restart_ntpd|ntp_conf_changed)::
      "$(service_cmd) ntpd restart" classes => if_repaired("ntpd_restarted");

  reports:
    ntpd_restarted::
      "cf3: ntpd has been restarted";
}
